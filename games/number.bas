10 REM Copied from http://www.vintage-basic.net/games.html
20 PRINT "NUMBER"
30 PRINT "CREATIVE COMPUTING MORRISTOWN, NEW JERSEY"
40 PRINT
50 PRINT
60 PRINT
70 PRINT "YOU HAVE 100 POINTS. BY GUESSING NUMBERS FROM 1 TO 5, YOU"
80 PRINT "CAN GAIN OR LOSE POINTS DEPENDING UPON HOW CLOSE YOU GET TO"
90 PRINT "A RANDOM NUMBER SELECTED BY THE COMPUTER."
100 PRINT
110 PRINT "YOU OCCASIONALLY WILL GET A JACKPOT WHICH WILL DOUBLE(!)"
120 PRINT "YOUR POINT COUNT. YOU WIN WHEN YOU GET 500 POINTS."
130 PRINT
140 LET P = 100
150 INPUT "GUESS A NUMBER FROM 1 TO 5" G
160 LET R = RND(5)
170 LET D = INT(ABS(R - G))
180 IF D = 0 GOTO 270
190 IF D = 1 OR D = 2 GOTO 350
200 REM Failed
210 PRINT "A MISS"
220 PRINT
230 LET P = P - 5
240 IF P > 0 GOTO 390
250 PRINT "YOU ARE OUT OF POINTS. TOO BAD!!!"
260 END
270 REM Guessed the number
280 LET J = RND(2)
290 IF J = 2 GOTO 320
300 LET P = P + 5
310 GOTO 390
320 PRINT "YOU HIT THE JACKPOT!!!"
330 LET P = P + P
340 GOTO 390
350 REM A Close guess
360 PRINT "A GOOD GUESS"
370 LET P = P + 1
380 GOTO 390
390 PRINT "YOU HAVE" P "POINTS"
400 IF P < 500 GOTO 150
410 PRINT "!!!!YOU WIN!!!! WITH" P "POINTS."
420 END