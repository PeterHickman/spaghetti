10 REM Copied from http://www.vintage-basic.net/games.html
20 PRINT " 23 MATCHES"
30 PRINT " CREATIVE COMPUTING MORRISTOWN, NEW JERSEY"
40 PRINT
50 PRINT
60 PRINT
70 PRINT " THIS IS A GAME CALLED '23 MATCHES'."
80 PRINT
90 PRINT "WHEN IT IS YOUR TURN, YOU MAY TAKE ONE, TWO, OR THREE"
100 PRINT "MATCHES. THE OBJECT OF THE GAME IS NOT TO HAVE TO TAKE"
110 PRINT "THE LAST MATCH."
120 PRINT
130 PRINT "LET'S FLIP A COIN TO SEE WHO GOES FIRST."
140 PRINT "IF IT COMES UP HEADS, I WILL WIN THE TOSS."
150 PRINT
160 REM
170 let N = 23
180 let Q = RND(2)
190 IF Q = 1 THEN 230
200 PRINT "TAILS! YOU GO FIRST. "
210 PRINT
220 GOTO 310
230 PRINT "HEADS! I WIN! HA! HA!"
240 PRINT "PREPARE TO LOSE, MEATBALL-NOSE!!"
250 PRINT
260 PRINT "I TAKE 2 MATCHES"
270 let N = N -2
280 PRINT "THE NUMBER OF MATCHES IS NOW" N
290 PRINT
300 PRINT "YOUR TURN -- YOU MAY TAKE 1, 2 OR 3 MATCHES."
310 PRINT "HOW MANY DO YOU WISH TO REMOVE" ;
320 INPUT K
330 IF K > 3 THEN 530
340 IF K <= 0 THEN 530
350 let N = N - K
360 PRINT "THERE ARE NOW" N "MATCHES REMAINING."
370 IF N = 4 THEN 440
380 IF N = 3 THEN 460
390 IF N = 2 THEN 480
400 IF N <= 1 THEN 630
410 let Z = 4 - K
420 GOTO 490
430 PRINT
440 let Z = 3
450 GOTO 490
460 let Z = 2
470 GOTO 490
480 let Z = 1
490 PRINT "MY TURN ! I REMOVE" Z "MATCHES"
500 let N = N - Z
510 IF N <= 1 THEN 570
520 GOTO 280
530 PRINT "VERY FUNNY! DUMMY!"
540 PRINT "DO YOU WANT TO PLAY OR GOOF AROUND?"
550 PRINT "NOW, HOW MANY MATCHES DO YOU WANT" ;
560 GOTO 320
570 PRINT
580 PRINT "YOU POOR BOOB! YOU TOOK THE LAST MATCH! I GOTCHA!!"
590 PRINT "HA ! HA ! I BEAT YOU !!!"
600 PRINT
610 PRINT "GOOD BYE LOSER!"
620 GOTO 660
630 PRINT "YOU WON, FLOPPY EARS !"
640 PRINT "THINK YOU'RE PRETTY SMART !"
650 PRINT "LETS PLAY AGAIN AND I'LL BLOW YOUR SHOES OFF !!"
660 END
