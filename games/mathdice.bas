10 REM Copied from http://www.vintage-basic.net/games.html
20 PRINT "MATH DICE"
30 PRINT "CREATIVE COMPUTING MORRISTOWN, NEW JERSEY"
50 PRINT
60 PRINT
70 PRINT "THIS PROGRAM GENERATES SUCCESIVE PICTURES OF TWO DICE."
80 PRINT "WHEN TWO DICE AND AN EQUAL SIGN FOLLOWED BY A QUESTION"
90 PRINT "MARK HAVE BEEN PRINTED, TYPE YOUR ANSWER AND THE RETURN KEY."
100 PRINT "TO CONCLUDE THE LESSON, TYPE CONTROL-C AS YOUR ANSWER."
110 PRINT
120 PRINT
130 LET N = 0
140 LET N = N + 1
150 LET D = RND(6)
160 PRINT " ----- "
170 IF D = 1 GOTO 240
180 IF D = 2 GOTO 220
190 IF D = 3 GOTO 220
200 PRINT "I * * I"
210 GOTO 250
220 PRINT "I  *  I"
230 GOTO 250
240 PRINT "I     I"
250 IF D = 2 GOTO 300
260 IF D = 4 GOTO 300
270 IF D = 6 GOTO 320
280 PRINT "I  *  I"
290 GOTO 330
300 PRINT "I     I"
310 GOTO 330
320 PRINT "I * * I"
330 IF D = 1 GOTO 400
340 IF D = 2 GOTO 380
350 IF D = 3 GOTO 380
360 PRINT "I * * I"
370 GOTO 410
380 PRINT "I  *  I"
390 GOTO 410
400 PRINT "I     I"
410 PRINT " ----- "
420 PRINT
430 IF N = 2 GOTO 480
440 PRINT "   +"
450 PRINT
460 LET A = D
470 GOTO 140
480 LET T = D + A
490 INPUT " = " T1
500 IF T1 = T GOTO 560
510 PRINT "NO, COUNT THE SPOTS AND GIVE ANOTHER ANSWER."
520 INPUT " =" T2
530 IF T2 = T GOTO 560
540 PRINT "NO, THE ANSWER IS" T
550 GOTO 570
560 PRINT "RIGHT!"
570 PRINT
580 PRINT "THE DICE ROLL AGAIN..."
590 PRINT
600 LET N = 0
610 GOTO 140
620 END